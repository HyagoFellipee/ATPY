MODEL PBMOD
comment-----------------------------------------------
Butterworth low pass filter order 0-3
Amplitude corrected for specified frequency ScaleFreq
--------------------------------------------endcomment
DATA   
  Gain        {dflt:1}   --gain of filter
  FilterFreq  {dflt:800} --anti-aliasing filter frequency
  FilterOrder {dflt:2}   --Order of Butterworth filter 0..3
  ScaleFreq   {dflt:50}  --Base frequency for scaling. 0=no scale
INPUT  
  X        -- monitored signal
OUTPUT 
  XF       -- filtered signal
VAR    
  XF,tau,n,alpha,beta,amp,arg
HISTORY xf {dflt:0}
INIT
  tau:=recip(2*PI*FilterFreq)
  beta:=ScaleFreq*recip(FilterFreq)
  --calculate amplitude scaling at power frequency
  alpha:=Gain*sqrt((1+beta**(2*FilterOrder)))
ENDINIT  
EXEC
  if FilterOrder=1 then
      claplace(XF/X):=(alpha|s0)/(1|s0+tau|s1)
    elsif FilterOrder=2 then
      claplace(XF/X):=(alpha|s0)/(1|s0+sqrt(2)*tau|s1+tau**2|s2)
    elsif FilterOrder=3 then
      claplace(XF/X):=(alpha|s0)/(1|s0+2*tau|s1+2*tau**2|s2+tau**3|s3)
    else
      XF:=Gain*X 
  endif
ENDEXEC
ENDMODEL